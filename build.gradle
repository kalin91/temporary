plugins {
    id 'java'
    id 'jacoco'
    id 'org.springframework.boot' version '3.5.11'
    id 'io.spring.dependency-management' version '1.1.4'
    id("com.netflix.dgs.codegen") version "8.3.0"

}

group = 'com.demo.portfolio'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = '21'
}

repositories {
    mavenCentral()
}

ext {
    set('netflixDgsVersion', "10.5.0")
    testFailures = []
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-security'

    implementation "com.netflix.graphql.dgs:graphql-dgs-reactive:${netflixDgsVersion}"
    implementation "com.netflix.graphql.dgs:graphql-dgs-spring-graphql:${netflixDgsVersion}"
    implementation "com.netflix.graphql.dgs:graphql-dgs-extended-scalars:${netflixDgsVersion}"
    implementation 'org.springframework.graphql:spring-graphql'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    implementation 'org.mapstruct:mapstruct:1.5.5.Final'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.5.Final'

    implementation 'com.h2database:h2'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'io.projectreactor:reactor-test'
    testImplementation "io.karatelabs:karate-junit5:1.5.2"
}
    def totalPassed = 0
    def totalFailed = 0
    def totalSkipped = 0
tasks.withType(Test).configureEach {
    useJUnitPlatform()
    testClassesDirs = sourceSets.test.output.classesDirs
    classpath = sourceSets.test.runtimeClasspath
    reports {
        html.required = false
        junitXml.required = true
    }
    ignoreFailures = true
    testLogging {
        showStandardStreams = true
    }
    afterSuite { desc, result ->
        if (desc.parent == null) {
            totalPassed += result.successfulTestCount
            totalFailed += result.failedTestCount
            totalSkipped += result.skippedTestCount
            println "Task '${name}' finished: ${result.resultType} (${result.testCount} tests, ${totalPassed} passed, ${totalFailed} failed, ${totalSkipped} skipped)"
            
            if (result.resultType == TestResult.ResultType.FAILURE) {
                println "FAILURE detected in task: ${name}"
                rootProject.ext.testFailures.add(name) // 'name' refers to the task name here
            }
        }
    }
}

tasks.register('karateTest', Test) {
    group = 'verification'
    description = 'Runs only ITKarateRunner integration test'
    include '**/ITKarateRunner*'
}

test {
    exclude '**/ITKarateRunner*'
    dependsOn karateTest
    finalizedBy tasks.named('jacocoTestReport')

}


tasks.register('mergedTestReport', TestReport) {
    destinationDirectory = layout.buildDirectory.dir("reports/tests/merged")
    testResults.from(test.binaryResultsDirectory, karateTest.binaryResultsDirectory)
}

tasks.register('verifyAllTests') {
    dependsOn mergedTestReport

    doLast {
        if (totalPassed + totalFailed + totalSkipped > 0) {
            println "\n=== Global Test Summary ==="
            println "Total Passed: ${totalPassed}"
            println "Total Failed: ${totalFailed}"
            println "Total Skipped: ${totalSkipped}"
            println "===========================\n"
        }
        if (totalFailed > 0) {
            throw new GradleException("Tests failed in the following tasks: ${rootProject.ext.testFailures}. See merged report.")
        } else if (totalPassed > 0) {
            println "All tests passed successfully."
        }
    }
}
mergedTestReport.finalizedBy verifyAllTests
check.dependsOn mergedTestReport

tasks.named('jacocoTestReport') {
    dependsOn tasks.named('test')
    classDirectories.setFrom(files(classDirectories.files.collect {
        fileTree(dir: it, excludes: ['com/demo/portfolio/api/generated/**'])
    }))
    reports {
        html.required = true
        html.outputLocation = layout.buildDirectory.dir('reports/tests/jacoco')
        xml.required = false
        csv.required = false
    }
}

generateJava {
    schemaPaths = ["${projectDir}/src/main/resources/schema"]
    packageName = 'com.demo.portfolio.api.generated'
    generateClient = true
}

sourceSets {
    test {
        resources {
            srcDir file('src/test/java')
            exclude '**/*.java'
        }
    }
}
